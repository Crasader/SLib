apply plugin: 'com.android.library'

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 12
        versionName "1.1"
        externalNativeBuild {
            cmake {
                targets 'slib', 'freetype', 'sqlite3', 'opus', 'vpx', 'zxing', 'hiredis'
                arguments '-DANDROID_ARM_MODE=arm', '-DANDROID_ARM_NEON=TRUE'
            }
        }
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
        consumerProguardFiles 'proguard-rules.pro'
    }
    buildTypes {
        debug {
            jniDebuggable true
        }
        release {
            minifyEnabled false
        }
    }
    sourceSets {
        main.java.srcDirs += '../../../java/android'
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

ext.SUPPORT_LIBRARY_VERSION = "27.1.1"

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    
    implementation project(':external_ShortcutBadger')
    implementation project(':slib_fcm')
    implementation project(':slib_call')

    implementation "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:customtabs:$SUPPORT_LIBRARY_VERSION"

    implementation "com.google.android.gms:play-services-ads:17.2.0"
    implementation "com.google.firebase:firebase-messaging:17.6.0"

    implementation "com.facebook.android:facebook-login:5.0.1"
    implementation "com.facebook.android:facebook-share:5.0.1"
}

tasks.all { task ->
    if (task.name == "assembleDebug" || task.name == "assembleRelease") {
        task.dependsOn(project(":external_ShortcutBadger").getTasksByName(task.name, true)[0])
        task.dependsOn(project(":slib_fcm").getTasksByName(task.name, true)[0])
        task.dependsOn(project(":slib_call").getTasksByName(task.name, true)[0])
        task.doLast {
            def copyAAR = { srcDir, srcFileName, dstFileName ->
                def sourceFile = projectDir.getPath() + "/" + srcDir + "/" + srcFileName + ".aar";
                def targetDir = projectDir.getPath() + "/../../../lib/Android";
                def targetFileName = dstFileName + ".aar";
                def targetFile = targetDir + "/" + targetFileName;
                new File(targetDir).mkdirs()
                new File(targetFile).delete();
                new File(sourceFile).renameTo(targetFile);
            }
            System.out.println("Moving slib.aar into lib!")
            copyAAR("build/outputs/aar", "slib", "slib")
            copyAAR("../slib_fcm/build/outputs/aar", "slib_fcm", "slib_fcm")
            copyAAR("../slib_call/build/outputs/aar", "slib_call", "slib_call")
            copyAAR("../../../external/java/ShortcutBadger/build/outputs/aar", "external_ShortcutBadger", "ShortcutBadger")
        }
    }
}